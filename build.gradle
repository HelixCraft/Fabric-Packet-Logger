plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = "${project.archives_base_name}-${project.active_mc_version}"
}

// --- DYNAMIC VERSION CONFIGURATION ---
// Supported versions: 1.21.5 - 1.21.8
def mcVersionData = [
    // Format: "MC_VERSION": ["YARN_MAPPINGS", "LOADER_VERSION", "FABRIC_API_VERSION"]
    "1.21.5":  ["1.21.5+build.1", "0.18.4", "0.128.2+1.21.5"],
    "1.21.6":  ["1.21.6+build.1", "0.18.4", "0.128.2+1.21.6"],
    "1.21.7":  ["1.21.7+build.8", "0.18.4", "0.129.0+1.21.7"],
    "1.21.8":  ["1.21.8+build.1", "0.18.4", "0.136.1+1.21.8"],
	"1.21.9":  ["1.21.9+build.1", "0.18.4", "0.134.1+1.21.9"],
	"1.21.10": ["1.21.10+build.3", "0.18.4", "0.138.4+1.21.10"],
    "1.21.11": ["1.21.11+build.4", "0.18.4", "0.141.0+1.21.11"]
]
def activeVersion = project.active_mc_version.trim()
if (!mcVersionData.containsKey(activeVersion)) {
    throw new GradleException("Unsupported Minecraft version: '${activeVersion}'. Supported versions: ${mcVersionData.keySet()}")
}

project.ext.minecraft_version = activeVersion
project.ext.yarn_mappings     = mcVersionData[activeVersion][0]
project.ext.loader_version    = mcVersionData[activeVersion][1]
project.ext.fabric_version    = mcVersionData[activeVersion][2]

println "Building for Minecraft ${project.minecraft_version} (Yarn: ${project.yarn_mappings}, Loader: ${project.loader_version}, API: ${project.fabric_version})"
// -------------------------------------


repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	
	// Cloth Config Repository
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases/"
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"packet-logger" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// Gson f√ºr JSON-Serialisierung (bereits in Minecraft enthalten)
	implementation "com.google.code.gson:gson:2.10.1"
}

processResources {
	inputs.property "version", project.version
	inputs.property "active_mc_version", project.minecraft_version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version, "active_mc_version": project.minecraft_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}